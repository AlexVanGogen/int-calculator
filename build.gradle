plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group 'edu.ifmo'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

apply plugin: 'antlr'
apply plugin: 'application'
apply plugin: 'distribution'

mainClassName = 'edu.ifmo.intexpr.CliKt'

configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-package", "edu.ifmo.intexpr.parser", "-visitor", "-long-messages"]
}
compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

sourceSets {
    main.java.srcDirs += "${project.buildDir}/generated-src/antlr/main"
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    antlr "org.antlr:antlr4:4.7"
    compile "org.antlr:antlr4-runtime:4.7"
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
}

